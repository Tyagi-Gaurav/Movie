plugins {
    id 'java'
    id "com.google.protobuf" version "0.8.17"
}

version 'unspecified'

repositories {
    mavenCentral()
}

ext {
    grpcVersion = '1.40.1'
    junitVersion = '5.7.2'
    javaxAnnotationVersion = '1.3.2'
}

dependencies {
    implementation "io.grpc:grpc-stub:${grpcVersion}"
    implementation "io.grpc:grpc-protobuf:${grpcVersion}"
    implementation "io.grpc:grpc-netty-shaded:${grpcVersion}"
    implementation "javax.annotation:javax.annotation-api:${javaxAnnotationVersion}"

    testImplementation "org.junit.jupiter:junit-jupiter-api:${junitVersion}"
    testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:${junitVersion}"
}

processResources {
    dependsOn(":grpcService:generateProto")
}

test {
    useJUnitPlatform()
}

protobuf {
    protoc {
        artifact = "com.google.protobuf:protoc:3.17.3"
    }
    plugins {
        grpc {
            artifact = 'io.grpc:protoc-gen-grpc-java:1.40.1'
        }
    }
    generateProtoTasks {
        all()*.plugins {
            grpc {
                outputSubDir = 'java'
            }
        }
    }
    generatedFilesBaseDir = "$projectDir/src/"
}