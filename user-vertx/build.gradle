plugins {
    id 'io.vertx.vertx-plugin' version '1.3.0'
    id "com.github.johnrengelman.shadow" version "7.1.2"
    id 'jacoco'
    id 'com.github.spotbugs' version '5.0.9'
    id 'pmd'
    id "org.gradle.test-retry" version "1.4.0"
}

repositories {
    mavenCentral()
}

vertx {
    mainVerticle = "com.gt.scr.user.ConfigVerticle"
    vertxVersion = '4.3.2'
}

ext {
    springBootVersion = '2.7.2'
    springVersion = '5.3.22'
    commonsLang3Version = '3.12.0'
    commonsTextVersion = '1.9'
    slf4jVersion = '2.0.0-alpha7'
    jacksonVersion = '2.13.3'
    jsonWebTokenVersion = '0.11.5'
    mysqlVersion = '8.0.29'
    c3p0Version = '0.9.5.5'
    liquibaseVersion = '4.13.0'
    guavaVersion = '31.1-jre'
    metricsInterceptor = '1.9.2'
    junitVersion = '5.8.2'
    assertJVersion = '3.23.1'
    h2DbVersion = '1.4.200'
    testContainerVersion = '1.17.3'
    mockitoVersion = '4.6.1'
    reactorVersion = '3.4.12'
    vertxVersion = '4.2.6'
    micronautVersion = '3.5.3'
    postgresVersion = '42.4.0'
    embeddedPostgresVersion = '1.0.1'
    bouncyCastleVersion = '1.70'
    scramVersion = '2.1'
    validationApiVersion = '2.0.1.Final'
    awaitilityVersion = '4.2.0'
}

sourceCompatibility = JavaVersion.VERSION_17
targetCompatibility = JavaVersion.VERSION_17

sourceSets {
    integration {
        java.srcDir "$projectDir/src/integration/java"
        compileClasspath += sourceSets.main.output + sourceSets.test.output
        runtimeClasspath += output + compileClasspath
    }
}

dependencies {
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.8.2'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.8.2'
}

jacoco {
    toolVersion = "0.8.7"
}

processResources {
    with copySpec {
        from 'src/main/resources'
        include '**/application*.yml'
        include '**/*.properties'
    }

    duplicatesStrategy = DuplicatesStrategy.INCLUDE
}

configurations {
    integrationImplementation.extendsFrom testImplementation
    integrationRuntime.extendsFrom testImplementation
}

dependencies {
    implementation project(":common")
    implementation "io.vertx:vertx-core:${vertxVersion}"
    implementation "io.vertx:vertx-web:${vertxVersion}"
    implementation "io.vertx:vertx-auth-jwt:${vertxVersion}"
    implementation "io.vertx:vertx-web-validation:${vertxVersion}"
    implementation "io.vertx:vertx-web-api-contract:${vertxVersion}"
    implementation "io.vertx:vertx-config:${vertxVersion}"
    implementation "io.vertx:vertx-pg-client:${vertxVersion}"
    implementation "io.vertx:vertx-web-openapi:${vertxVersion}"
    implementation "io.vertx:vertx-auth-jwt:${vertxVersion}"
    implementation "io.vertx:vertx-health-check:${vertxVersion}"
    implementation "io.micronaut:micronaut-inject:${micronautVersion}"
    implementation "io.micronaut:micronaut-inject-java:${micronautVersion}"
    annotationProcessor "io.micronaut:micronaut-inject-java:${micronautVersion}"
    implementation "org.postgresql:postgresql:${postgresVersion}"
    implementation "org.bouncycastle:bcprov-jdk15on:${bouncyCastleVersion}"
    implementation "com.ongres.scram:common:${scramVersion}"
    implementation "com.ongres.scram:client:${scramVersion}"
    implementation "javax.validation:validation-api:${validationApiVersion}"

    implementation "org.apache.commons:commons-lang3:${commonsLang3Version}"
    implementation "org.apache.commons:commons-text:${commonsTextVersion}"
    implementation "org.slf4j:slf4j-log4j12:${slf4jVersion}"

    implementation "com.fasterxml.jackson.datatype:jackson-datatype-jsr310:${jacksonVersion}"
    implementation "com.fasterxml.jackson.core:jackson-core:${jacksonVersion}"
    implementation "com.fasterxml.jackson.core:jackson-databind:${jacksonVersion}"
    implementation "com.fasterxml.jackson.core:jackson-annotations:${jacksonVersion}"

    implementation "io.jsonwebtoken:jjwt-api:${jsonWebTokenVersion}"
    implementation "io.jsonwebtoken:jjwt-impl:${jsonWebTokenVersion}"
    implementation "io.jsonwebtoken:jjwt-jackson:${jsonWebTokenVersion}"

    implementation "mysql:mysql-connector-java:${mysqlVersion}"
    implementation "com.mchange:c3p0:${c3p0Version}"
    implementation "org.liquibase:liquibase-core:${liquibaseVersion}"

    implementation "com.google.guava:guava:${guavaVersion}"
    implementation "io.micrometer:micrometer-core:${metricsInterceptor}"

    testImplementation "org.mockito:mockito-core:$mockitoVersion"
    testImplementation "org.springframework.boot:spring-boot-starter-test:${springBootVersion}"
    testImplementation "org.springframework.boot:spring-boot-test-autoconfigure:${springBootVersion}"
    testImplementation "org.springframework.boot:spring-boot-starter-webflux:${springBootVersion}"
    testImplementation "org.springframework:spring-test:${springVersion}"
    testImplementation "org.junit.jupiter:junit-jupiter-api:${junitVersion}"
    testImplementation "org.junit.jupiter:junit-jupiter-engine:${junitVersion}"
    testImplementation "org.assertj:assertj-core:${assertJVersion}"
    testImplementation "org.testcontainers:junit-jupiter:${testContainerVersion}"
    testImplementation "org.testcontainers:mysql:${testContainerVersion}"
    testImplementation "io.vertx:vertx-junit5:${vertxVersion}"
    testImplementation "com.opentable.components:otj-pg-embedded:${embeddedPostgresVersion}"
    testImplementation "org.awaitility:awaitility:${awaitilityVersion}"
    spotbugsPlugins 'com.h3xstream.findsecbugs:findsecbugs-plugin:1.12.0'
}

pmd {
    consoleOutput = true
    toolVersion = "6.37.0"
    rulesMinimumPriority = 2
    ruleSetFiles = files("${projectDir}/gradle/pmd/pmd-rules.xml")
    reportsDir = layout.buildDirectory.dir('pmd').get().asFile
}

test {
    retry {
        maxRetries = 2
        maxFailures = 5
    }

    useJUnitPlatform()
    finalizedBy jacocoTestReport
}

jacocoTestReport {
    dependsOn test
    reports {
        xml.enabled true
        csv.enabled false
        html.enabled true
        html.destination layout.buildDirectory.dir('jacocoHtml').get().asFile
    }
    afterEvaluate {
        classDirectories.setFrom(files(classDirectories.files.collect {
            fileTree(dir: it, exclude: [
                    "com/gt/scr/user/Application.class",
                    "com/gt/scr/user/config/**/*.class",
                    "com/gt/scr/user/ConfigVerticle.class",
                    "com/gt/scr/user/resource/domain/*.class"
            ])
        }))
    }

    executionData.from = files('build/jacoco/test.exec', 'build/jacoco/integrationTest.exec')
}

spotbugs {
    ignoreFailures = false
    excludeFilter = file("gradle/spotbugs/exclusions.xml")
}

spotbugsMain {
    reports {
        html {
            required = true
            outputLocation = file("$buildDir/reports/spotbugs/main/spotbugs.html")
            stylesheet = 'fancy-hist.xsl'
        }
    }
}

spotbugsTest {
    reports {
        html {
            required = true
            outputLocation = file("$buildDir/reports/spotbugs/test/spotbugs.html")
            stylesheet = 'fancy-hist.xsl'
        }
    }
}

spotbugsIntegration {
    reports {
        html {
            required = true
            outputLocation = file("$buildDir/reports/spotbugs/integration/spotbugs.html")
            stylesheet = 'fancy-hist.xsl'
        }
    }
}

tasks.register('integrationTest', Test) {
    description = 'Runs integration tests.'
    group = 'verification'

    testClassesDirs = sourceSets.integration.output.classesDirs
    classpath += sourceSets.integration.runtimeClasspath

    retry {
        maxRetries = 2
        maxFailures = 5
    }

    useJUnitPlatform()

    reports {
        html.enabled true
        junitXml.enabled = true
    }
    shouldRunAfter test
    finalizedBy jacocoTestReport
}


check.dependsOn(integrationTest)